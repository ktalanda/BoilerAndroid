apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'me.tatarka.retrolambda'
apply from: '../config/quality.gradle'

def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
def buildTime = new Date().format("yyyy-MM-dd'T'HH:mm:ss'Z'", TimeZone.getTimeZone("UTC"))
def generateVersion() {
    def time = new Date().format "yyMMddHHmm"
    def repo = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
    return "${time}-${repo}"
}

def KEY_ALIAS_APP = hasProperty('KEY_ALIAS') ? KEY_ALIAS : 'androiddebugkey'
def KEY_PASSWORD_APP = hasProperty('KEY_PASSWORD') ? KEY_PASSWORD : 'android'
def KEYSTORE_FILE_APP = hasProperty('KEYSTORE_FILE') ? KEYSTORE_FILE : '../dev_tools/debug.keystore'
def KEYSTORE_PASSWORD_APP = hasProperty('KEYSTORE_PASSWORD') ? KEYSTORE_PASSWORD : 'android'

project.ext {
    versionName = generateVersion()
    versionCode = 1
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.1"

    signingConfigs {
        app {
            keyAlias KEY_ALIAS_APP
            keyPassword KEY_PASSWORD_APP
            storeFile file(KEYSTORE_FILE_APP)
            storePassword KEYSTORE_PASSWORD_APP
        }
    }

    defaultConfig {
        applicationId "pl.k2net.boilerandroid"
        minSdkVersion 21
        targetSdkVersion 23
        versionCode project.versionCode
        versionName project.versionName

        signingConfig signingConfigs.app

        buildConfigField "String", "GIT_SHA", "\"${gitSha}\""
        buildConfigField "String", "BUILD_TIME", "\"${buildTime}\""
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "String", "HOST", "\"\""
        }
        debug {
            applicationIdSuffix '.debug'
            buildConfigField "String", "HOST", "\"\""
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'LICENSE.txt'
    }

}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    apt (
            'org.projectlombok:lombok:1.12.6',
    )
    provided(
            'javax.annotation:jsr250-api:1.0',
            'com.google.dagger:dagger-compiler:2.0.2',
            'org.projectlombok:lombok:1.12.6',
    )
    compile(
            'com.android.support:appcompat-v7:23.1.1',
            'com.google.dagger:dagger:2.0.2',
            'com.jakewharton:butterknife:7.0.1',
            'com.jakewharton.timber:timber:4.1.0',
            'com.squareup.retrofit:retrofit:2.0.0-beta2',
            'io.reactivex:rxjava:1.1.0',
            'io.reactivex:rxandroid:1.1.0',
            'com.squareup.retrofit:adapter-rxjava:2.0.0-beta2',
    )
    testCompile(
            'junit:junit:4.12',
    )
    androidTestCompile(

    )
    retrolambdaConfig 'net.orfjackal.retrolambda:retrolambda:2.1.0'
    compile 'com.android.support:design:23.1.1'
}
